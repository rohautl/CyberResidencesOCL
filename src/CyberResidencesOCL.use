-------------------------------------------------------------------------------
--                           CyberResidencesOCL.use
-------------------------------------------------------------------------------

model CyberResidences


enum Category {
    economy,
    standard,
    premium,
    prestige
}


enum Gender {
    male,
    female
}


class Residence
attributes
    name     : String
    floorMin : Integer
    floorMax : Integer
    category : Category
    maxNbOfFreeUnits : Integer -- @derived
    avgRate : Real             -- @derived
operations
    -- Set of bedrooms of the residence.
    bedrooms() : Set(Bedroom) =
        rooms->select(oclIsKindOf(Bedroom))->oclAsType(Set(Bedroom))
    -- Set of useful bedrooms.
    usefulBedrooms() : Set(Bedroom) =
        bedrooms()->select(not isOutOfOrder)
    -- Set of residence bathrooms
    bathrooms() : Set(Bathroom) =
        rooms->select(oclIsKindOf(Bathroom))->oclAsType(Set(Bathroom))
end


class Room
attributes
    number : Integer
    floor : Integer -- @derived
    isOutOfOrder : Boolean
end


class Bathroom < Room
attributes
    isOnTheLanding : Boolean -- @derived
end

-- TODO
-------------

class Bedroom < Room
attributes
	nbOfSingleBeds :Integer
	nbOfDoubleBeds : Integer
	rate : Real
	isNonSmoking : Boolean
	nbOfUnits : Integer -- @derived
end

associationclass Rent
between
	Bedroom[1..*] role rentedBedrooms
	Tenant[0..1] role tenant
attributes
	discount : Real -- @derived
	rate : Real -- @derived
end

class Discount
attributes	
	percentage : Integer
	label : String
end

class Tenant < Resident
attributes
	paidRate : Real -- @derived
end

class Resident < Person
attributes
	isSmoker : Boolean
end

class Person
attributes
	name : String
	age : Integer
	gender : Gender
end

association IsInBedroom
between
	Bathroom[0..3] role bathrooms
	Bedroom[0..1] role bedroom
end

association occupiedRoom
between
	Bedroom[1] role IsOccupiedBy
	Resident[*] role occupants
end

association IsMarriedWith
between	
	Resident[0..1] role consort
	Resident[0..1]
end

association IsTutoredBy
between	
	Resident[0..2] role tutors
	Resident[*] role tutored
end

association ContainsRooms
between
	Residence[1] role residence
	Room[*] role rooms
end

association HasDiscounts
between	
	Rent[1] role rent
	Discount[*] role discounts
end

-------------

-- For association classes use the following syntax
-- (see documentation for more details)
-- associationclass ... between
--     ...[...] role ...
--     ...[...] role ...
-- attributes
--     ...
-- end


constraints

-------------------------------------------------------------------------------
--                               aboooConditions
-------------------------------------------------------------------------------
-- Conditions for granting a discount "All Bathrooms Out Of Order" (ABOOO).
-- Note Easy. Use 'one'. Should world with 'xxxPercentage'

context Rent inv aboooConditions:
    -- A discount is given for bedrooms that have all bathrooms out of
    -- order. The label of this type of discount is 'BROOO50'. This
    -- rule does not apply in economy residences.

	if self.rentedBedrooms.residence.category = Category::economy then true else
		if self.rentedBedrooms.bathrooms->forAll(b : Bathroom | b.isOutOfOrder = true) then
		self.discounts->one(label='BROOO50') else true endif
	endif

-------------------------------------------------------------------------------
--                               aboooPercentage
-------------------------------------------------------------------------------
-- Percentage of "ABOOO" discount according to residence category.
-- Not Easy. Should work with the corresponding invariant 'xxxConditions'

context Discount inv aboooPercentage:
    -- ABOO discounts are equal to 50%.

	if self.label='ABOO' then self.percentage = 50 else true endif

-------------------------------------------------------------------------------
--                                 adultMarried
-------------------------------------------------------------------------------
-- Only adult married.
-- Easy. Use 'implies'

context Resident inv adultMarried:
    -- To be registered as married, resident must be adult.

	self.consort->notEmpty() implies self.age >= 18

-------------------------------------------------------------------------------
--                                 adultTenant
-------------------------------------------------------------------------------
-- Tenants are adults.
-- Easy

context Tenant inv adultTenant:
    -- The minimal age to rent a bedroom is 18 years old.

	self.age >= 18 

-------------------------------------------------------------------------------
--                             allPrestigeBathrooms
-------------------------------------------------------------------------------
-- Invariant about all bathrooms for 'prestige' residence.
-- Medium. use 'implies'.

context Residence inv allPrestigeBathrooms:
    -- Residences of category prestige have in each bedroom at least
    -- the same number of bathroom that the number of beds.

	self.category = Category::prestige implies 
	self.bathrooms()->size() >= self.bedrooms()->size()
-------------------------------------------------------------------------------
--                                 atLeastABed
-------------------------------------------------------------------------------
-- At least one bed for useful bedrooms.
-- Easy. Use implies.

context Bedroom inv atLeastABed:
    -- Bedrooms that are not out of order must have at least one bed.

	not isOutOfOrder implies nbOfSingleBeds > 0 or nbOfDoubleBeds > 0	
	
-------------------------------------------------------------------------------
--                               atLeastOneAdult
-------------------------------------------------------------------------------
-- At least one adult per room.
-- Easy. Use 'implies'

context Bedroom inv atLeastOneAdult:
    -- At least one adult must be presented in all occupied rooms.

		self.occupants->notEmpty() implies self.occupants -> exists(e:Person|e.age >= 18)

-------------------------------------------------------------------------------
--                               childNonSmoking
-------------------------------------------------------------------------------
-- Non smoking child.
-- Easy


context Resident inv childNonSmoking:
    -- Children are always declared as non smoker.

	self.age <= 18 implies isSmoker = false

-------------------------------------------------------------------------------
--                                 childTutored
-------------------------------------------------------------------------------
-- Child tutored.
-- Medium

context Resident inv childTutored:
    -- Resident that are less than 18 years old must have at least one
    -- tutor, apart in prestige residences where this rule does not
    -- apply.
	if self.IsOccupiedBy.residence.category<>Category::prestige then
		self.age< 18 implies self.tutors->notEmpty()
	else true endif
	
-------------------------------------------------------------------------------
--                              cumulatedDiscounts
-------------------------------------------------------------------------------
-- Limit of cumulated discounts.
-- Easy. Use 'sum'

context Rent inv cumulatedDiscounts:
    -- No room will have more that 70% of discounts cumulated.

	self.discounts.percentage->sum() < 70
	
-------------------------------------------------------------------------------
--                                deriveAvgRate
-------------------------------------------------------------------------------
-- Derivation of the avgRate attribute.
-- Medium

context Residence inv deriveAvgRate:
    -- The average rate of a residence is the total revenue of the
    -- residence divided by the number of occupants. Tutored residents
    -- do not count in this rule.
	--Set bedrooms() : Set(Bedroom) =
    --    rooms->select(oclIsKindOf(Bedroom))->oclAsType(Set(Bedroom))
	self.avgRate=self.bedrooms().rent.rate->sum()/self.bedrooms().occupants->select(e| e.tutors->isEmpty())->size()

-------------------------------------------------------------------------------
--                                deriveDiscount
-------------------------------------------------------------------------------
-- Derivation of the attribute discount.
-- Easy

context Rent inv deriveDiscount:
    -- The discount for a rent, is the rate of the bedroom multiplied
    -- by the sum of percentages of all discounts.

	self.discount=self.rentedBedrooms.rate*self.discounts.percentage->sum()/100
	
-------------------------------------------------------------------------------
--                                 deriveFloor
-------------------------------------------------------------------------------
-- Derivation of the attribute floor.
-- Easy

context Room inv deriveFloor:
    -- The floor number correspond to the first digits of the room
    -- number, where the last two digits are removed. For instance the
    -- room 634 is at the floor 6.

	self.floor = self.number div 100

-------------------------------------------------------------------------------
--                             deriveIsOnTheLanding
-------------------------------------------------------------------------------
-- Derivation of isOnTheLanding attribute.
-- Easy

context Bathroom inv deriveIsOnTheLanding:
    -- A bathroom is said to be on the landing if it is not in a
    -- bedroom.

	if  self.bedroom->isEmpty() then
	self.isOnTheLanding = true else self.isOnTheLanding = false endif

-------------------------------------------------------------------------------
--                            deriveMaxNbOfFreeUnits
-------------------------------------------------------------------------------
-- Derivation of maxNbOfFreeUnits attribute.
-- Easy

context Residence inv deriveMaxNbOfFreeUnits:
    -- The maximum number of free units is equal to the maximum number
    -- of places in beds of all rooms of the residence.

	self.maxNbOfFreeUnits = self.bedrooms().nbOfUnits->sum()

-------------------------------------------------------------------------------
--                               deriveNbOfUnits
-------------------------------------------------------------------------------
-- Derivation of nbOfUnits attribute.
-- Easy

context Bedroom inv deriveNbOfUnits:
    -- The number of units of a bedroom is the maximum number of
    -- people that can fit within the bedroom assuming that all places
    -- in all beds are full.

	self.nbOfUnits=self.nbOfDoubleBeds * 2 + self.nbOfSingleBeds

-------------------------------------------------------------------------------
--                                derivePaidRate
-------------------------------------------------------------------------------
-- Derivation of the attribute paidRate.
-- Not Easy. Use 'if ... then ... else ... endif'   and
-- 'let groupFloors : Set(Integer) = ... in ...'

context Tenant inv derivePaidRate:
    -- The total amount paid by a tenant is the sum of rent rates for
    -- all bedrooms rented. If the members of his or her group is
    -- spread over more than one floor, then 20â‚¬ of reduction will
    -- automatically be deduced.
	
	--let groupFloor : Set(Integer) = self.rentedBedrooms in rate
		if (self.rentedBedrooms->forAll(e1,e2 : Bedroom |e1.floor=e2.floor ) ) then
			self.paidRate = self.rent.rate->sum() else
				self.paidRate = self.rent.rate->sum()-20
		endif
	

-------------------------------------------------------------------------------
--                                  deriveRate
-------------------------------------------------------------------------------
-- Derivation of the attribute rate.
-- Easy

 context Rent inv deriveRate:
    -- The rate of a rent corresponds to the regular rate for the room
    -- minus the discount.

	self.rate = self.rentedBedrooms.rate - self.discount

-------------------------------------------------------------------------------
--                               domainPercentage
-------------------------------------------------------------------------------
-- Domain of percentage attribute.
-- Easy

 context Discount inv domainPercentage:
    -- The percentage is a value between 1 and 100.

	self.percentage >= 1 and self.percentage <=100

-------------------------------------------------------------------------------
--                          enoughBathroomsInPrestige
-------------------------------------------------------------------------------
-- At least as many bathrooms than beds for prestige residence.
-- Easy. Use implies.

 context Bedroom inv enoughBathroomsInPrestige:
    -- In a prestige residence, the number of private bathrooms is at
    -- least the same as the number of beds in each bedroom.

	self.residence.category= Category::prestige implies 
	self.bathrooms.isOnTheLanding->count(false) >= (self.nbOfDoubleBeds + self.nbOfSingleBeds)
	

-------------------------------------------------------------------------------
--                            floorBetweenMinAndMax
-------------------------------------------------------------------------------
-- Floor between minimum and maximum floors.
-- Easy

context Room inv floorBetweenMinAndMax:
    -- The floor of a room is between the minium and maximum floor of
    -- the residence.

	self.floor <= self.residence.floorMax and self.floor >= self.residence.floorMin
	

-------------------------------------------------------------------------------
--                                  floorOrder
-------------------------------------------------------------------------------
-- Order between floorMin and floorMax attributes.
-- Easy

 context Residence inv floorOrder:
    -- The maximum floor is at higher or at the same level that the
    -- minimum floor of the residence.

	self.floorMax >= self.floorMin

-------------------------------------------------------------------------------
--                                 labelUnique
-------------------------------------------------------------------------------
-- Uniqueness of labels with respect to rents.
-- Easy. Use 'unique'

 context Rent inv labelUnique:
    -- All discounts associated to a rent have distinct labels.

	self.discounts->isUnique(label)

-------------------------------------------------------------------------------
--                           marriageAntiReflexivity
-------------------------------------------------------------------------------
-- Anti-reflexivity of marriage relationship.
-- Easy

context Resident inv marriageAntiReflexivity:
    -- A resident cannot mary with himself or herself.

	self.consort <> self

-------------------------------------------------------------------------------
--                             marriageInSameGroup
-------------------------------------------------------------------------------
-- Consorts in the same group.
-- Medium

context Resident inv marriageInSameGroup:
    -- A resident and his or her consort occupies a room of the same
    -- group.

	self.consort->notEmpty implies self.tutors = self.consort.tutors
	
	
-------------------------------------------------------------------------------
--                               marriageSymmetry
-------------------------------------------------------------------------------
-- Symmetry of marriage relationship.
-- Easy

 context Resident inv marriageSymmetry:
    -- If a resident is declared to be the consort of another
    -- resident, then the opposite is true as well.

	self.consort->notEmpty() implies self.consort.consort = self 

-------------------------------------------------------------------------------
--                              noBedroomOverload
-------------------------------------------------------------------------------
-- No bedroom overloaded.
-- Medium

 context Bedroom inv noBedroomOverload:
    -- There must be no more people in a bedroom that the number of
    -- units of this bedroom. Children up to 3 years old are not
    -- counted in this rule.
	
	self.occupants->select(age > 3)->size() <= self.nbOfUnits

-------------------------------------------------------------------------------
--                            noEmptyRentedBedrooms
-------------------------------------------------------------------------------
-- Rented bedrooms not empty.
-- Easy. Use 'implies'

context Bedroom inv noEmptyRentedBedrooms:
    -- All rented bedrooms are occupied by at least one occupant.

	self.tenant->notEmpty() implies self.occupants->notEmpty()

-------------------------------------------------------------------------------
--                                  noSquaters
-------------------------------------------------------------------------------
-- Nobody in rooms not rented.
-- Easy. Use 'implies'

context Bedroom inv noSquaters:
    -- Occupants must be exclusively in rooms that are rented by
    -- somebody.

	 self.occupants->notEmpty() implies self.tenant->notEmpty()

	 
-------------------------------------------------------------------------------
--                               noTutorForAdults
-------------------------------------------------------------------------------
-- No tutor for adults.
-- Easy

context Resident inv noTutorForAdults:
    -- Adults never have tutor.

	self.age>=18 implies self.tutors->isEmpty()
 

-------------------------------------------------------------------------------
--                             oooBedroomsNotRented
-------------------------------------------------------------------------------
-- No rental of Out Of Order bedrooms.
-- Easy

context Bedroom inv oooBedroomsNotRented:
    -- Bedrooms that are out of order cannot be rented.

	self.isOutOfOrder = true implies self.tenant->isEmpty()

-------------------------------------------------------------------------------
--                                oooIfPrestige
-------------------------------------------------------------------------------
-- Out of order bathroom implications if prestige. 
-- Medium. Use 'implies'

context Bathroom inv oooIfPrestige:
    -- In prestige residences, it is not acceptable to rent bedrooms
    -- with bathrooms that are out of order. So if a bathrom is marked
    -- as out of order, the corresponding bedroom is also marked as
    -- out of order.

	self.residence.category = Category::prestige implies
	( self.isOutOfOrder = true implies self.bedroom.isOutOfOrder = true )

-------------------------------------------------------------------------------
--                              rentInOneResidence
-------------------------------------------------------------------------------
-- Medium. Should be rather short.
-- <Enter note text here>

context Tenant inv rentInOneResidence:
    -- For a given tenant, all rented bedrooms are in the same
    -- residence.

	self.rentedBedrooms->forAll(e1,e2: Bedroom | e1.residence = e2.residence)

-------------------------------------------------------------------------------
--                             sameNumberIfPrivate
-------------------------------------------------------------------------------
-- Same number if private.
-- Easy. Use 'implies'.

context Bathroom inv sameNumberIfPrivate:
    -- All bathrooms that are not on the landing, that is that are
    -- private to a bedroom, have the same number of room as this
    -- bedroom.

	self.isOnTheLanding = false implies self.number = self.bedroom.number 

-------------------------------------------------------------------------------
--                               sboooConditions
-------------------------------------------------------------------------------
-- Conditions for granting a discount "Some Bathrooms Out Of Order" (SBOOO).
-- Not Easy. Use 'one'. Should world with 'xxxPercentage'

context Rent inv sboooConditions:
    -- A discount if given for a bed room if there are "Some Bathrooms
    -- Out Of Order", that is for bedroom with at least one bathroom
    -- out of order, but at least one bathroom still available.  The
    -- label for this type of discount is SBOO. This type of discount
    -- does not apply on economy residences.

	self.rentedBedrooms.residence.category <> Category::economy
		and self.rentedBedrooms.bathrooms->exists(e:Bathroom|e.isOutOfOrder = true)
		and self.discounts -> exists(e:Discount|e.label = 'SBOO') implies 
	--		self.rentedBedrooms->forAll(e:Bedroom|e->one(isOutOfOrder = true))
		self.rentedBedrooms.isOutOfOrder=true

-------------------------------------------------------------------------------
--                               sboooPercentage
-------------------------------------------------------------------------------
-- Percentage of "SBOOO" discount according to residence category.
-- Not Easy. Should work with the corresponding invariant 'xxxConditions'

context Discount inv sboooPercentage:
    -- In premium residences, SBOO discounts are equal to 25%. For
    -- standard category the discount is of 10%.

	if self.label = 'SBOO' then 
		if self.rent.rentedBedrooms.residence.category = 
		Category::premium then self.percentage = 25 else 
			if self.rent.rentedBedrooms.residence.category = 
			Category::standard then self.percentage = 10 else
			true endif
		endif
	else true endif
		
		

-------------------------------------------------------------------------------
--                             smokingPremiumPolicy
-------------------------------------------------------------------------------
-- Smoking policy for premium residences.
-- Not easy

context Bedroom inv smokingPremiumPolicy:
    -- In a premium residence a resident that specified that he or she
    -- is non smoker will never ever be place in a smoking room.

	self.residence.category = Category::premium and self.isNonSmoking = false 
	implies self.occupants->forAll(e:Resident|e.isSmoker = true)

-------------------------------------------------------------------------------
--                            smokingPrestigePolicy
-------------------------------------------------------------------------------
-- No smoking is prestige.
-- Easy

context Bedroom inv smokingPrestigePolicy:
    -- There are no smoking rooms at all in residence of prestige.

	self.residence.category = Category::prestige implies self.isNonSmoking = true

-------------------------------------------------------------------------------
--                              tenantInRentedRoom
-------------------------------------------------------------------------------
-- Room occupied by the tenant.
-- Medium. 

context Tenant inv tenantInRentedRoom:
    -- A tenant occupies one of the bedroom that he or she has rented.

	self.IsOccupiedBy->notEmpty() implies 
	self.rentedBedrooms->exists(e:Bedroom| e = self.IsOccupiedBy)

-------------------------------------------------------------------------------
--                                  tutorAdult
-------------------------------------------------------------------------------
-- Tutors are adults.
-- Easy

context Resident inv tutorAdult:
    -- A resident can not be the tutor of another resident if he or
    -- she is not an adult.
	
	self.age>=18 and self.tutored->notEmpty() 
	implies self.tutored->forAll(e:Resident|e.age<18)

-------------------------------------------------------------------------------
--                               tutorInSameGroup
-------------------------------------------------------------------------------
-- Tutors and tutored in one group.
-- Medium

context Resident inv tutorInSameGroup:
    -- The tutors of a resident are in the same group that this
    -- resident. In other words they are all in the same group of
    -- rooms.
	
	self.tutored->notEmpty() implies 
		self.tutored->forAll(r : Resident | r.IsOccupiedBy = self.IsOccupiedBy)

-------------------------------------------------------------------------------
--                     uniqueNumberApartForPrivateBathroom
-------------------------------------------------------------------------------
-- Invariant on room numbering.
-- Medium

context Residence inv uniqueNumberApartForPrivateBathroom:
    -- All rooms have a distinct number, except private bathrooms.

	self.rooms->forAll(e1,e2 : Room | (e1<>e2 implies e1.number <> e2.number) or
		e1.oclIsKindOf(Bathroom) or e2.oclIsKindOf(Bathroom) )